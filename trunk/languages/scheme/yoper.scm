; Ским и комбинаторы неподвижной точки.
; Ским-тексты от David Espinosa, espinosa@cs.columbia.edu.
; Комментарии Сергея Вакуленко, vak@kiae.su.

; Пусть G - целочисленная функция целого аргумента.
; Определим функционал F такой, что:
;   F [G] (0) = 1
;   F [G] (1) = G (0)
;   F [G] (2) = 2 * G (1)
;   F [G] (3) = 3 * G (2)
;   ...
; Функционал F преобразует целочисленную функцию целого аргумента
; в другую функцию.  Существует неподвижная точка функционала,
; т.е. функция, которая преобразуется сама в себя.  Для F неподвижной
; точкой является функция факториала. 

(define f
  (lambda (g)
    (lambda (n)
      (if (= 0 n) 1
	(* n (g (- n 1)))
) ) ) )

; Существуют функционалы, т.н. комбинаторы неподвижной точки,
; вычисляющие для каждого функционала соответствующую неподвижную точку:
;   Y [F] есть такая функция G, что 
;   F [G] (x) = G (x)
; Один из комбинаторов неподвижной точки, Y-комбинатор,
; приведен ниже.

(define Y
  (lambda (f)
    ((lambda (g) (f (lambda (arg) ((g g) arg))))
     (lambda (g) (f (lambda (arg) ((g g) arg))))
) ) )

; Применение Y к F дает функцию факториала, например:
; ((Y f) 5) дает результат 120.

(display ((Y f) 5)) (newline)
(display ((Y f) 10)) (newline)
