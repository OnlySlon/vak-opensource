;
; Беззнаковое умножение 16x16
; ~~~~~~~~~~~~~~~~~~~~~~~~~~~
; Вход:		%A0 - мл. байт 1-го множ-ля
;		%A1 - ст. байт 1-го множ-ля
;		%A2 - мл. байт 2-го множ-ля
;		%A3 - ст. байт 2-го множ-ля
; Выход:	%A2 - байт 0 рез-та
;		%A3 - байт 1 рез-та
;		%A4 - байт 2 рез-та
;		%A5 - байт 3 рез-та
; Исп-ся:	%A6 - счётчик сдвигов
;
mul16u:
	clr	%a5		; Инициализация байта 3 результата
	xor	%a4, %a4	; Инициализация байта 2 результата и CF
	movl	%a6, 17		; Число битов в байте + 1
	jmp	$4f		; На нулевой сдвиг 2-го множителя
$2:	add	%a4, %a0	; Сложение байта 2 результата с мл. байтом 1-го множ-ля
	adc	%a5		; Сложение байта 3 результата с CF
	jnc	$100f		; Если не установлен CF
	add	%a5, %a1	; Сложение байта 3 результата с ст. байтом 1-го множ-ля
	sst	1		; Восстановление
	jmp	$3f		;  CF
$100:	add	%a5, %a1	; Сложение байта 3 результата с ст. байтом 1-го множ-ля
$3:	rrc	%a5		; Циклич. сдвиг вправо байта 3 результата
	rrc	%a4		; Циклич. сдвиг вправо байта 2 результата
$4:	rrc	%a3		; Циклич. сдвиг вправо байта 1 рез-та и мл.б. 2 множ-ля
	rrc	%a2		; Циклич. сдвиг вправо байта 0 рез-та и ст.б. 2 множ-ля
	jc	$5f		; Если CF=1
	dec	%a6		; Пока не все
	jnz	$3b		;  сдвиги
	rts			; Выход из подпрограммы
$5:	dec	%a6		; Пока не все
	sst	1		; CF=1
	jnz	$2b		;  сдвиги
	rts			; Выход из подпрограммы
