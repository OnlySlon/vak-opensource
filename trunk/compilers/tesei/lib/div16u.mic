;
; Беззнаковое деление 16/16
; ~~~~~~~~~~~~~~~~~~~~~~~~~
; Вход:		%A0 - мл. байт делимого
;		%A1 - ст. байт делимого
;		%A2 - мл. байт делителя
;		%A3 - ст. байт делителя
; Выход:	%A0 - мл. байт частного
;		%A1 - ст. байт частного
;		%A4 - мл. байт остатка
;		%A5 - ст. байт остатка
; Исп-ся:	%A6 - счётчик сдвигов
;
div16u:
	clr	%a4		; Инициализация мл. байта остатка
	xor	%a5, %a5	; Инициализация ст. байта остатка и CF
	movl	%a6, 17		; Число битов в байте + 1
$1:	rlc	%a0		; Циклич. сдвиг влево мл.б. делимого и мл.б. частного
	rlc	%a1		; Циклич. сдвиг влево ст.б. делимого и ст.б. частного

	jc	$4f		; Если CF=1
	dec	%a6		; Пока не все
	jnz	$2f		;  сдвиги
	rts			; Выход из подпрограммы
$4:	dec	%a6		; Пока не все
	sst	1		; CF=1
	jnz	$2f		;  сдвиги
	rts			; Выход из подпрограммы
$2:	rlc	%a4		; Циклич. сдвиг влево мл. байта остатка
	rlc	%a5		; Циклич. сдвиг влево ст. байта остатка
	sub	%a4, %a2	; Вычитание из мл.б. остатка мл.б. делителя
	sbc	%a5		; Вычитание из ст.б. остатка CF
	jc	$5f		; Если разность <0
	sub	%a5, %a3	; Вычитание из ст.б. остатка ст.б. делителя
	jnc	$3f		; Если разность >0
	add	%a4, %a2	; Иначе -
	adc	%a5		;  восстанавливается
	add	%a5, %a3	;  остаток
	cst	1		; CF=0
	jmp	$1b		; Следующий цикл
$3:	sst	1		; CF=1
	jmp	$1b		; Следующий цикл
$5:	add	%a4, %a2	; Восстанавливается
	adc	%a5		;  остаток
	cst	1		; CF=0
	jmp	$1b		; Следующий цикл
