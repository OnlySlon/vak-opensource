;
; Знаковое умножение 8x8
; ~~~~~~~~~~~~~~~~~~~~~~
; Вход:		%A0 - 1-ый множитель
;		%A1 - 2-ой множитель
; Выход:	%A2 - ст. байт рез-та
;		%A1 - мл. байт рез-та
; Исп-ся:	%A3 - счётчик сдвигов
;
mul8s:
	xor	%a2, %a2	; Инициализация ст. байта результата и CF
	movl	%a3, 8		; Число битов в байте
	jmp	$3f		; На битовую проверку
$2:	add	%a2, %a0	; Сложение ст. байта результата с 1-ым множителем
$3:	bttl	%a1, 0001b	; Установлен бит 0 в мл. байте рез-та и 2-го множ-ле?
	jz	.+1		; Нет
	sub	%a2, %a0	; Восстановление ст. байта результата
	shra	%a2		; Арифметич. сдвиг вправо ст. байта результата
	rrc	%a1		; Циклич. сдвиг вправо мл. байта рез-та и 2-го множ-ля
	jc	$5f		; Если CF=1
	dec	%a3		; Пока не все
	jnz	$3b		;  сдвиги
	rts			; Выход из подпрограммы
$5:	dec	%a3		; Пока не все
	sst	1		; CF=1
	jnz	$2b		;  сдвиги
	rts			; Выход из подпрограммы
